/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.CommSq

/-!

# HomLift

Given a functor `p : ğ’³ â¥¤ ğ’®`, this file provides API for expressing the fact that `p(Ï†) = f`
for a given arrow `Ï†` in `ğ’³` and a given arrow `f` in `ğ’®`. This makes it possible to also
express such equalities even when the domain and codomain of `f` are not definitionally equal
to ....

The notion `p(Ï†) = f` is captured in the structure `IsHomLift p Ï† f`, which consists of

-/

universe uâ‚ vâ‚ uâ‚‚ vâ‚‚

open CategoryTheory Functor Category

variable {ğ’® : Type uâ‚} {ğ’³ : Type uâ‚‚} [Category ğ’³] [Category ğ’®]

/-- The proposition that an arrow a --Ï†--> b lifts an arrow R --f--> S in ğ’® via p. This is
often drawn as:
```
  a --Ï†--> b
  -        -
  |        |
  v        v
  R --f--> S
``` -/
structure IsHomLift (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) : Prop where
  (ObjLiftDomain : p.obj a = R)
  (ObjLiftCodomain : p.obj b = S)
  (HomLift : CommSq (p.map Ï†) (eqToHom ObjLiftDomain) (eqToHom ObjLiftCodomain) f)

namespace IsHomLift

-- TODO: better names for these
protected lemma hom_eq {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    (hÏ† : IsHomLift p f Ï†) : f = eqToHom hÏ†.ObjLiftDomain.symm â‰« p.map Ï† â‰« 
      eqToHom hÏ†.ObjLiftCodomain :=
  ((eqToHom_comp_iff hÏ†.ObjLiftDomain _ _).1 hÏ†.HomLift.w.symm)

protected lemma hom_eq' {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b} 
    (hÏ† : IsHomLift p f Ï†) : p.map Ï† = eqToHom hÏ†.ObjLiftDomain â‰« f â‰« 
      eqToHom hÏ†.ObjLiftCodomain.symm := by
  rw [â† assoc, â† comp_eqToHom_iff hÏ†.ObjLiftCodomain _ _]
  exact hÏ†.HomLift.w

lemma eq_of_isHomLift {p : ğ’³ â¥¤ ğ’®} (a b : ğ’³) {f : p.obj a âŸ¶ p.obj b} {Ï† : a âŸ¶ b}
    (hÏ† : IsHomLift p f Ï†) : f = p.map Ï† := by
  simpa using IsHomLift.hom_eq hÏ†

/-- For any arrow `Ï† : a âŸ¶ b` in `ğ’³`, `Ï†` lifts the arrow `p.map Ï†` in the base `ğ’®`-/
@[simp]
protected lemma self (p : ğ’³ â¥¤ ğ’®) {a b : ğ’³} (Ï† : a âŸ¶ b) : IsHomLift p (p.map Ï†) Ï† where
  ObjLiftDomain := rfl
  ObjLiftCodomain := rfl
  HomLift := âŸ¨by simp only [eqToHom_refl, comp_id, id_comp]âŸ©

@[simp]
protected lemma id {p : ğ’³ â¥¤ ğ’®} {R : ğ’®} {a : ğ’³} (ha : p.obj a = R) : IsHomLift p (ğŸ™ R) (ğŸ™ a) :=
  ha â–¸ (p.map_id _ â–¸ IsHomLift.self p (ğŸ™ a))

protected lemma comp {p : ğ’³ â¥¤ ğ’®} {R S T : ğ’®} {a b c : ğ’³} {f : R âŸ¶ S}
  {g : S âŸ¶ T} {Ï† : a âŸ¶ b} {Ïˆ : b âŸ¶ c} (hÏ† : IsHomLift p f Ï†)
  (hÏˆ : IsHomLift p g Ïˆ) : IsHomLift p (f â‰« g) (Ï† â‰« Ïˆ) where
    ObjLiftDomain := hÏ†.1
    ObjLiftCodomain := hÏˆ.2
    HomLift := (p.map_comp _ _).symm â–¸ CommSq.horiz_comp hÏ†.3 hÏˆ.3

/-- If `Ï† : a âŸ¶ b` and `Ïˆ : b âŸ¶ c` lift `ğŸ™ S`, then so does `Ï† â‰« Ïˆ` -/
lemma lift_id_comp {p : ğ’³ â¥¤ ğ’®} {R : ğ’®} {a b c : ğ’³} {Ï† : a âŸ¶ b} {Ïˆ : b âŸ¶ c} (hÏ† : IsHomLift p (ğŸ™ R) Ï†)
  (hÏˆ : IsHomLift p (ğŸ™ R) Ïˆ) : IsHomLift p (ğŸ™ R) (Ï† â‰« Ïˆ) :=
  comp_id (ğŸ™ R) â–¸ IsHomLift.comp hÏ† hÏˆ

/-- If `Ï† : a âŸ¶ b` lifts `f` and `Ïˆ : b âŸ¶ c` lifts `ğŸ™ T`, then `Ï†  â‰« Ïˆ` lifts `f` -/
lemma comp_lift_id_right {p : ğ’³ â¥¤ ğ’®} {R S T : ğ’®} {a b c : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} (hÏ† : IsHomLift p f Ï†) {Ïˆ : b âŸ¶ c} (hÏˆ : IsHomLift p (ğŸ™ T) Ïˆ) :
    IsHomLift p f (Ï† â‰« Ïˆ) where
  ObjLiftDomain := hÏ†.ObjLiftDomain
  ObjLiftCodomain := by rw [hÏˆ.ObjLiftCodomain, â† hÏˆ.ObjLiftDomain, hÏ†.ObjLiftCodomain]
  HomLift := âŸ¨by simp [IsHomLift.hom_eq' hÏˆ, hÏ†.3.1]âŸ©

-- TODO: I think this lemma is uncessarry? Since T above could be S or sth different, it doesnt matter
/-- If `Ï† : a âŸ¶ b` lifts `ğŸ™ S` and `Ïˆ : b âŸ¶ c` lifts `f`, then `Ï†  â‰« Ïˆ` lifts `f` -/
lemma comp_lift_id_left {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b c : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} (hÏ† : IsHomLift p f Ï†) {Ïˆ : b âŸ¶ c} (hÏˆ : IsHomLift p (ğŸ™ S) Ïˆ) :
    IsHomLift p f (Ï† â‰« Ïˆ) where
  ObjLiftDomain := hÏ†.ObjLiftDomain
  ObjLiftCodomain := by rw [â† hÏ†.ObjLiftCodomain, hÏˆ.ObjLiftDomain, hÏˆ.ObjLiftCodomain]
  HomLift := âŸ¨by simp [IsHomLift.hom_eq' hÏˆ, hÏ†.3.1]âŸ©

@[simp]
lemma eqToHom_domain_lift_id {p : ğ’³ â¥¤ ğ’®} {a b : ğ’³} (hab : a = b) {R : ğ’®}
    (hR : p.obj a = R) : IsHomLift p (ğŸ™ R) (eqToHom hab) where
      ObjLiftDomain := hR
      ObjLiftCodomain := hab â–¸ hR
      HomLift := âŸ¨by simp only [eqToHom_map, eqToHom_trans, comp_id]âŸ©

@[simp]
lemma eqToHom_codomain_lift_id {p : ğ’³ â¥¤ ğ’®} {a b : ğ’³} (hab : a = b) {S : ğ’®}
    (hS : p.obj b = S) : IsHomLift p (ğŸ™ S) (eqToHom hab) where
      ObjLiftDomain := hab â–¸ hS
      ObjLiftCodomain := hS
      HomLift := âŸ¨by simp only [eqToHom_map, eqToHom_trans, comp_id]âŸ©

@[simp]
lemma id_lift_eqToHom_domain {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} (hRS : R = S)
    {a : ğ’³} (ha : p.obj a = R) : IsHomLift p (eqToHom hRS) (ğŸ™ a) where
      ObjLiftDomain := ha
      ObjLiftCodomain := hRS â–¸ ha
      HomLift := âŸ¨by simp only [map_id, id_comp, eqToHom_trans]âŸ©

@[simp]
lemma id_lift_eqToHom_codomain {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} (hRS : R = S)
    {b : ğ’³} (hb : p.obj b = S) : IsHomLift p (eqToHom hRS) (ğŸ™ b) where
      ObjLiftDomain := hRS â–¸ hb
      ObjLiftCodomain := hb
      HomLift := âŸ¨by simp only [map_id, id_comp, eqToHom_trans]âŸ©

@[simp]
lemma comp_eqToHom_lift_iff {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a' a b : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} {h : a' = a} : IsHomLift p f (eqToHom h â‰« Ï†) â†” IsHomLift p f Ï† where
  mp := by intro hÏ†'; subst h; simpa using hÏ†'
  mpr := fun hÏ† => id_comp f â–¸ IsHomLift.comp (eqToHom_codomain_lift_id h hÏ†.ObjLiftDomain) hÏ†

@[simp]
lemma eqToHom_comp_lift_iff {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b b' : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} {h : b = b'} : IsHomLift p f (Ï† â‰« eqToHom h) â†” IsHomLift p f Ï† where
  mp := by intro hÏ†'; subst h; simpa using hÏ†'
  mpr := fun hÏ† => comp_id f â–¸ IsHomLift.comp hÏ† (eqToHom_domain_lift_id h hÏ†.ObjLiftCodomain)

@[simp]
lemma lift_eqToHom_comp_iff {p : ğ’³ â¥¤ ğ’®} {R' R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} (h : R' = R) : IsHomLift p ((eqToHom h) â‰« f) Ï† â†” IsHomLift p f Ï† where
  mp := by intro hÏ†'; subst h; simpa using hÏ†'
  mpr := fun hÏ† =>
    id_comp Ï† â–¸ IsHomLift.comp (IsHomLift.id_lift_eqToHom_codomain h hÏ†.ObjLiftDomain) hÏ†

@[simp]
lemma lift_comp_eqToHom_iff {p : ğ’³ â¥¤ ğ’®} {R S S': ğ’®} {a b : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} (h : S = S') : IsHomLift p (f â‰« (eqToHom h)) Ï† â†” IsHomLift p f Ï† where
  mp := by intro hÏ†'; subst h; simpa using hÏ†'
  mpr := fun hÏ† =>
    comp_id Ï† â–¸ IsHomLift.comp hÏ† (IsHomLift.id_lift_eqToHom_domain h hÏ†.ObjLiftCodomain)

/-- The isomorphism `R â‰… S` obtained from an isomorphism `Ï† : a â‰… b` lifting `f` -/
-- TODO: better name
def Iso_of_lift_Iso {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a â‰… b}
   (hÏ† : IsHomLift p f Ï†.hom) : R â‰… S :=
  eqToIso hÏ†.ObjLiftDomain.symm â‰ªâ‰« p.mapIso Ï† â‰ªâ‰« eqToIso hÏ†.ObjLiftCodomain

@[simp]
lemma Iso_of_lift_Iso_hom {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a â‰… b}
   (hÏ† : IsHomLift p f Ï†.hom) : (Iso_of_lift_Iso hÏ†).hom = f := by
  simp [Iso_of_lift_Iso, IsHomLift.hom_eq hÏ†]

@[simp]
lemma Iso_of_lift_Iso_comp {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a â‰… b}
    (hÏ† : IsHomLift p f Ï†.hom) : (Iso_of_lift_Iso hÏ†).inv â‰« f = ğŸ™ S := by
  rw [CategoryTheory.Iso.inv_comp_eq]
  simp only [Iso_of_lift_Iso_hom, comp_id]

@[simp]
lemma comp_Iso_of_lift_Iso {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a â‰… b}
    (hÏ† : IsHomLift p f Ï†.hom) : f â‰« (Iso_of_lift_Iso hÏ†).inv = ğŸ™ R := by
  rw [CategoryTheory.Iso.comp_inv_eq]
  simp only [Iso_of_lift_Iso_hom, id_comp]

/-- If `Ï† : a âŸ¶ b` lifts `f : R âŸ¶ S` and `Ï†` is an isomorphism, then so is `f`. -/
lemma IsIso_of_lift_IsIso {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    (hÏ† : IsHomLift p f Ï†) [IsIso Ï†] : IsIso f :=
  (IsHomLift.hom_eq hÏ†) â–¸ inferInstance

-- TODO: Better names for these lemmas, e.g. `inv_lift_inv` and `inv_lift_inv_IsIso`?
-- TODO: these shouldnt need to assume that base is an isomorphism...
/-- Given `Ï† : a â‰… b` and `f : R â‰… S`, such that `Ï†.hom` lifts `f.hom`, then `Ï†.inv` lifts `f.inv`. -/
protected lemma inv_iso {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R â‰… S} {Ï† : a â‰… b}
    (hÏ† : IsHomLift p f.hom Ï†.hom) : IsHomLift p f.inv Ï†.inv where
  ObjLiftDomain := hÏ†.2
  ObjLiftCodomain := hÏ†.1
  HomLift := CommSq.horiz_inv (f:=p.mapIso Ï†) (i:=f) hÏ†.3

protected lemma inv_iso' {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a â‰… b}
    (hÏ† : IsHomLift p f Ï†.hom) : IsHomLift p (Iso_of_lift_Iso hÏ†).inv Ï†.inv where
  ObjLiftDomain := hÏ†.2
  ObjLiftCodomain := hÏ†.1
  HomLift := CommSq.horiz_inv (f:=p.mapIso Ï†) (i:=Iso_of_lift_Iso hÏ†) (by simpa using hÏ†.3)

/-- If `Ï† : a âŸ¶ b` lifts `f : R âŸ¶ S` and both are isomorphisms, then `Ï†â»Â¹` lifts `fâ»Â¹`. -/
-- TODO: this is slightly annoying since you need to "separately" prove that f is an iso...
protected lemma inv {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    (hÏ† : IsHomLift p f Ï†) [IsIso Ï†] [IsIso f] : IsHomLift p (inv f) (inv Ï†) :=
  IsHomLift.inv_iso (f:=asIso f) (Ï†:= asIso Ï†) hÏ†

/-- If `Ï† : a âŸ¶ b` is an isomorphism, and lifts `ğŸ™ S` for some `S : ğ’®`, then `Ï†â»Â¹` also lifts `ğŸ™ S` -/
lemma lift_id_inv {p : ğ’³ â¥¤ ğ’®} {S : ğ’®} {a b : ğ’³} {Ï† : a âŸ¶ b} [IsIso Ï†]
    (hÏ† : IsHomLift p (ğŸ™ S) Ï†) : IsHomLift p (ğŸ™ S) (inv Ï†) :=
  (IsIso.inv_id (X:=S)) â–¸ IsHomLift.inv hÏ†

end IsHomLift
