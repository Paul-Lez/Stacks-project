/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.CommSq
import Mathlib.CategoryTheory.Functor.Const
import Mathlib.CategoryTheory.Limits.Shapes.Pullbacks

/-!

# Fibered categories

This file defines what it means for a functor `p : ğ’³ â¥¤ ğ’®` to be fibered`.

## Main definitions

- `IsHomLift p f Ï†` expresses that a morphism `Ï†` in `ğ’³` is a lift of a morphism `f` in `ğ’®`
along the functor `p`. This class is introduced to deal with the issues related to equalities of
morphisms in a category.
- `IsPullback p f Ï†` expresses that `Ï†` is a pullback of `f` along `p`.
- `IsFibered p` expresses that `p` gives `ğ’³` the structure of a fibered category over `ğ’®`,
i.e. that for every morphism `f` in `ğ’®` and every object `a` in `ğ’³` there is a pullback of `f`
with domain `a`.

## Implementation

-/

/-
TODO:
-- TODO: naming... Open a namespace!
-- TODO: naming of variables, make it more consistent.
-/

universe uâ‚ vâ‚ uâ‚‚ vâ‚‚ uâ‚ƒ w

open CategoryTheory Functor Category

variable {ğ’® : Type uâ‚} {ğ’³ : Type uâ‚‚} [Category ğ’³] [Category ğ’®]

def HomLift' {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b)
 (ha : p.obj a = R) (hb : p.obj b = S) : Prop :=
  CommSq (p.map Ï†) (eqToHom ha) (eqToHom hb) f

/-- The proposition that an arrow a --Ï†--> b lifts an arrow R --f--> S in ğ’® via p. This is
often drawn as:
```
  a --Ï†--> b
  -        -
  |        |
  v        v
  R --f--> S
``` -/
class IsHomLift (p : ğ’³ â¥¤ ğ’®) {R S : ğ’®} {a b : ğ’³} (f : R âŸ¶ S) (Ï† : a âŸ¶ b) : Prop where
  (ObjLiftDomain : p.obj a = R)
  (ObjLiftCodomain : p.obj b = S)
  (HomLift : CommSq (p.map Ï†) (eqToHom ObjLiftDomain) (eqToHom ObjLiftCodomain) f)

/-- For any arrow `Ï† : a âŸ¶ b` in `ğ’³`, `Ï†` lifts the arrow `p.map Ï†` in the base `ğ’®`-/
@[simp]
lemma IsHomLift_self (p : ğ’³ â¥¤ ğ’®) {a b : ğ’³} (Ï† : a âŸ¶ b) : IsHomLift p (p.map Ï†) Ï† where
  ObjLiftDomain := rfl
  ObjLiftCodomain := rfl
  HomLift := âŸ¨by simp only [eqToHom_refl, comp_id, id_comp]âŸ©

@[simp]
lemma IsHomLift_id {p : ğ’³ â¥¤ ğ’®} {R : ğ’®} {a : ğ’³} (ha : p.obj a = R) : IsHomLift p (ğŸ™ R) (ğŸ™ a) :=
  ha â–¸ (p.map_id _ â–¸ IsHomLift_self p (ğŸ™ a))

lemma IsHomLift_congr {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    (hÏ† : IsHomLift p f Ï†) : eqToHom hÏ†.ObjLiftDomain.symm â‰« p.map Ï† â‰« eqToHom hÏ†.ObjLiftCodomain = f :=
  (eqToHom_comp_iff hÏ†.ObjLiftDomain.symm _ _).2 hÏ†.HomLift.w


lemma IsHomLift_congr' {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    (hÏ† : IsHomLift p f Ï†) : eqToHom hÏ†.ObjLiftDomain â‰« f â‰« eqToHom hÏ†.ObjLiftCodomain.symm = p.map Ï† := by
  rw [â†assoc, comp_eqToHom_iff hÏ†.ObjLiftCodomain.symm _ _]
  exact hÏ†.HomLift.w.symm

/-- If `Ï† : a âŸ¶ b` lifts `f : R âŸ¶ S` and `Ï†` is an isomorphism, then so is `f`. -/
-- TODO: Iso version of this?
lemma IsIsoofIsHomliftisIso {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
  (hlift : IsHomLift p f Ï†) [IsIso Ï†] : IsIso f := by
  rcases hlift with âŸ¨domain, _, âŸ¨homliftâŸ©âŸ©
  rw [â†eqToHom_comp_iff domain.symm] at homlift
  rw [â†homlift]
  exact IsIso.comp_isIso

/-- If `Ï† : a âŸ¶ b` lifts `f : R âŸ¶ S` and both are isomorphisms, then `Ï†â»Â¹` lifts `fâ»Â¹`. -/
lemma IsHomLift_inv {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b : ğ’³} {f : R âŸ¶ S} {Ï† : a âŸ¶ b}
    (hlift : IsHomLift p f Ï†) [IsIso Ï†] [IsIso f] : IsHomLift p (inv f) (inv Ï†) where
  ObjLiftDomain := hlift.2
  ObjLiftCodomain := hlift.1
  HomLift := by
    constructor
    simp only [map_inv, IsIso.eq_comp_inv, assoc, IsIso.inv_comp_eq]
    exact hlift.3.1.symm

/-- If `Ï† : a âŸ¶ b` is an isomorphism, and lifts `ğŸ™ S` for some `S : ğ’®`, then `Ï†â»Â¹` also lifts `ğŸ™ S` -/
lemma IsHomLift_inv_id {p : ğ’³ â¥¤ ğ’®} {S : ğ’®} {a b : ğ’³} {Ï† : a âŸ¶ b} [IsIso Ï†]
    (hlift : IsHomLift p (ğŸ™ S) Ï†) : IsHomLift p (ğŸ™ S) (inv Ï†) :=
  (IsIso.inv_id (X:=S)) â–¸ IsHomLift_inv hlift

-- TODO MOVE THIS UP SOMEWHAT
lemma IsHomLift_comp {p : ğ’³ â¥¤ ğ’®} {R S T : ğ’®} {a b c : ğ’³} {f : R âŸ¶ S}
  {g : S âŸ¶ T} {Ï† : a âŸ¶ b} {Ïˆ : b âŸ¶ c} (hÏ† : IsHomLift p f Ï†)
  (hÏˆ : IsHomLift p g Ïˆ) : IsHomLift p (f â‰« g) (Ï† â‰« Ïˆ) where
    ObjLiftDomain := hÏ†.1
    ObjLiftCodomain := hÏˆ.2
    HomLift := by
      -- TODO: could use composition of CommSq (once mathlib is updated)
      constructor
      rw [â†Category.assoc, â†hÏ†.3.1]
      simp only [map_comp, assoc, hÏˆ.3.1]

/-- If `Ï† : a âŸ¶ b` and `Ïˆ : b âŸ¶ c` lift `ğŸ™ S`, then so does `Ï† â‰« Ïˆ` -/
lemma IsHomLift_id_comp {p : ğ’³ â¥¤ ğ’®} {R : ğ’®} {a b c : ğ’³} {Ï† : a âŸ¶ b} {Ïˆ : b âŸ¶ c} (hÏ† : IsHomLift p (ğŸ™ R) Ï†)
  (hÏˆ : IsHomLift p (ğŸ™ R) Ïˆ) : IsHomLift p (ğŸ™ R) (Ï† â‰« Ïˆ) := by
  have := IsHomLift_comp hÏ† hÏˆ
  rw [comp_id] at this
  exact this

-- TODO: figure out naming scheme for these (or just comment better? e.g. IsHomLift_id_eqToHom_domain)
lemma IsHomLift_id_eqToHom {p : ğ’³ â¥¤ ğ’®} {a b : ğ’³} (hba : b = a) {S : ğ’®}
    (hS : p.obj a = S) : IsHomLift p (ğŸ™ S) (eqToHom hba) where
      ObjLiftDomain := hba â–¸ hS
      ObjLiftCodomain := hS
      HomLift := âŸ¨by simp only [eqToHom_map, eqToHom_trans, comp_id]âŸ©

lemma IsHomLift_id_eqToHom' {p : ğ’³ â¥¤ ğ’®} {a b : ğ’³} (hba : b = a) {S : ğ’®}
    (hS : p.obj b = S) : IsHomLift p (ğŸ™ S) (eqToHom hba) where
      ObjLiftDomain := hS
      ObjLiftCodomain := hba â–¸ hS
      HomLift := âŸ¨by simp only [eqToHom_map, eqToHom_trans, comp_id]âŸ©

lemma IsHomLift_eqToHom_id {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} (hRS : R = S)
    {a : ğ’³} (ha : p.obj a = S) : IsHomLift p (eqToHom hRS) (ğŸ™ a) where
      ObjLiftDomain := hRS â–¸ ha
      ObjLiftCodomain := ha
      HomLift := âŸ¨by simp only [map_id, id_comp, eqToHom_trans]âŸ©

lemma IsHomLift_eqToHom_id' {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} (hRS : R = S)
    {a : ğ’³} (ha : p.obj a = R) : IsHomLift p (eqToHom hRS) (ğŸ™ a) where
      ObjLiftDomain := ha
      ObjLiftCodomain := hRS â–¸ ha
      HomLift := âŸ¨by simp only [map_id, id_comp, eqToHom_trans]âŸ©


@[simp]
lemma IsHomLift_comp_eqToHom {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b c: ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} {hca : c = a} : IsHomLift p f (eqToHom hca â‰« Ï†) â†” IsHomLift p f Ï† where
  mp := by intro hÏ†'; subst hca; simpa using hÏ†'
  mpr := fun hÏ† => id_comp f â–¸ IsHomLift_comp (IsHomLift_id_eqToHom hca hÏ†.ObjLiftDomain) hÏ†

@[simp]
lemma IsHomLift_eqToHom_comp {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b c: ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} {hbc : b = c} : IsHomLift p f (Ï† â‰« eqToHom hbc) â†” IsHomLift p f Ï† where
  mp := by intro hÏ†'; subst hbc; simpa using hÏ†'
  mpr := fun hÏ† => comp_id f â–¸ IsHomLift_comp hÏ† (IsHomLift_id_eqToHom' hbc hÏ†.ObjLiftCodomain)

@[simp]
lemma IsHomLift_eqToHom_comp' {p : ğ’³ â¥¤ ğ’®} {R S T: ğ’®} {a b : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} (hTR : T = R) : IsHomLift p ((eqToHom hTR) â‰« f) Ï† â†” IsHomLift p f Ï† where
  mp := by intro hÏ†'; subst hTR; simpa using hÏ†'
  mpr := fun hÏ† => id_comp Ï† â–¸ IsHomLift_comp (IsHomLift_eqToHom_id hTR hÏ†.ObjLiftDomain) hÏ†

@[simp]
lemma IsHomLift_comp_eqToHom' {p : ğ’³ â¥¤ ğ’®} {R S T: ğ’®} {a b : ğ’³} {f : R âŸ¶ S}
    {Ï† : a âŸ¶ b} (hST : S = T) : IsHomLift p (f â‰« (eqToHom hST)) Ï† â†” IsHomLift p f Ï† where
  mp := by intro hÏ†'; subst hST; simpa using hÏ†'
  mpr := fun hÏ† => comp_id Ï† â–¸ IsHomLift_comp hÏ† (IsHomLift_eqToHom_id' hST hÏ†.ObjLiftCodomain)

-- TODO: move this elsewhere in this file (probably up a bit, to belong with the `id` lemmas...!)
/-- If `Ï† : a âŸ¶ b` lifts `f` and `Ïˆ : b âŸ¶ c` lifts `ğŸ™ T`, then `Ï†  â‰« Ïˆ` lifts `f` -/
lemma IsHomLift_of_IsHomLiftId_comp {p : ğ’³ â¥¤ ğ’®} {R S T : ğ’®} {a b c : ğ’³} {f : R âŸ¶ S}
    {Ï† : b âŸ¶ a} (hÏ† : IsHomLift p f Ï†) {Ïˆ : c âŸ¶ b} (hÏˆ : IsHomLift p (ğŸ™ T) Ïˆ) :
    IsHomLift p f (Ïˆ â‰« Ï†) where
  ObjLiftDomain := by
    rw [â†hÏ†.ObjLiftDomain, hÏˆ.ObjLiftCodomain, hÏˆ.ObjLiftDomain]
  ObjLiftCodomain := hÏ†.ObjLiftCodomain
  HomLift := âŸ¨by
    have : p.map Ïˆ = eqToHom (_ : p.obj c = p.obj b) := by simpa [comp_eqToHom_iff] using hÏˆ.3.1
    rw [map_comp, assoc, hÏ†.3.1, this, eqToHom_trans_assoc] âŸ©

/-- If `Ï† : a âŸ¶ b` lifts `ğŸ™ S` and `Ïˆ : b âŸ¶ c` lifts `f`, then `Ï†  â‰« Ïˆ` lifts `f` -/
lemma IsHomLift_of_comp_IsHomLiftId {p : ğ’³ â¥¤ ğ’®} {R S : ğ’®} {a b c : ğ’³} {f : R âŸ¶ S}
    {Ï† : b âŸ¶ a} (hÏ† : IsHomLift p f Ï†) {Ïˆ : a âŸ¶ c} (hÏˆ : IsHomLift p (ğŸ™ S) Ïˆ) :
    IsHomLift p f (Ï† â‰« Ïˆ) where
  ObjLiftDomain := hÏ†.ObjLiftDomain
  ObjLiftCodomain := by
    rw [â†hÏ†.ObjLiftCodomain, hÏˆ.ObjLiftDomain, hÏˆ.ObjLiftCodomain]
  HomLift := âŸ¨by
    have : p.map Ïˆ = eqToHom (_ : p.obj a = p.obj c) := by simpa [comp_eqToHom_iff] using hÏˆ.3.1
    rw [map_comp, assoc, this, eqToHom_trans, hÏ†.3.1] âŸ©
