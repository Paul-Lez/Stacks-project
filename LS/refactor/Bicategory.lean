import LS.refactor.Basic

import Mathlib.CategoryTheory.Bicategory.Strict
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.ConcreteCategory.Bundled

/-!
In this file give the type `BasedCategory ğ’®` the structure of a (strict) bicategory.

-/

universe uâ‚ vâ‚ uâ‚‚ vâ‚‚

open CategoryTheory Functor Category NatTrans Fibered

namespace Based

variable {ğ’® : Type uâ‚} [Category.{vâ‚} ğ’®]

/- 1-Morphisms

-/

structure Morphism (ğ’³ ğ’´ : BasedCategory ğ’®) extends CategoryTheory.Functor ğ’³.1 ğ’´.1 where
  (w : toFunctor â‹™ ğ’´.p = ğ’³.p)

@[simps!]
protected def Morphism.id (ğ’³ : BasedCategory ğ’®) : Morphism ğ’³ ğ’³ :=
  { ğŸ­ ğ’³.1 with w := CategoryTheory.Functor.id_comp _ }

@[simps!]
def Morphism.comp {ğ’³ ğ’´ ğ’µ : BasedCategory ğ’®} (F : Morphism ğ’³ ğ’´)
    (G : Morphism ğ’´ ğ’µ) : Morphism ğ’³ ğ’µ :=
  { F.toFunctor â‹™ G.toFunctor with w := by rw [Functor.assoc, G.w, F.w] }

@[simp]
lemma Morphism.assoc {ğ’³ ğ’´ ğ’µ ğ’¯ : BasedCategory ğ’®} (F : Morphism ğ’³ ğ’´) (G : Morphism ğ’´ ğ’µ)
    (H : Morphism ğ’µ ğ’¯) : Morphism.comp (Morphism.comp F G) H = Morphism.comp F (Morphism.comp G H) := rfl

@[simp]
lemma Morphism.comp_id {ğ’³ ğ’´ : BasedCategory ğ’®} (F : Morphism ğ’³ ğ’´) : Morphism.comp (Morphism.id ğ’³) F = F := rfl

@[simp]
lemma Morphism.id_comp {ğ’³ ğ’´ : BasedCategory ğ’®} (F : Morphism ğ’³ ğ’´) : Morphism.comp F (Morphism.id ğ’´) = F := rfl

@[simp]
lemma Morphism.obj_proj {ğ’³ ğ’´ : BasedCategory ğ’®} (F : Morphism ğ’³ ğ’´) (a : ğ’³.1) :
    ğ’´.p.obj (F.obj a) = ğ’³.p.obj a := by
  rw [â†Functor.comp_obj, F.w]

lemma Morphism.IsHomLift_map{ğ’³ ğ’´ : BasedCategory ğ’®} (F : Morphism ğ’³ ğ’´)
    {a b : ğ’³.1} (Ï† : a âŸ¶ b) : IsHomLift ğ’´ (ğ’³.p.map Ï†) (F.map Ï†) where
  ObjLiftDomain := Morphism.obj_proj F a
  ObjLiftCodomain := Morphism.obj_proj F b
  HomLift := âŸ¨by simp [congr_hom F.w.symm]âŸ©

lemma Morphism.pres_IsHomLift {ğ’³ ğ’´ : BasedCategory ğ’®} (F : Morphism ğ’³ ğ’´)
    {R S : ğ’®} {a b : ğ’³.1} {Ï† : a âŸ¶ b} {f : R âŸ¶ S} (hÏ† : IsHomLift ğ’³ f Ï†) : IsHomLift ğ’´ f (F.map Ï†) where
  ObjLiftDomain := Eq.trans (Morphism.obj_proj F a) hÏ†.ObjLiftDomain
  ObjLiftCodomain := Eq.trans (Morphism.obj_proj F b) hÏ†.ObjLiftCodomain
  HomLift := âŸ¨by
    rw [â†Functor.comp_map, congr_hom F.w]
    simp [hÏ†.3.1] âŸ©

lemma Morphism.HomLift_ofImage {ğ’³ ğ’´ : BasedCategory ğ’®} (F : Morphism ğ’³ ğ’´) {S R : ğ’®} {a b : ğ’³.1}
    {Ï† : a âŸ¶ b} {f : R âŸ¶ S} (hÏ† : IsHomLift ğ’´ f (F.map Ï†)) : IsHomLift ğ’³ f Ï† where
  ObjLiftDomain := F.obj_proj a â–¸ hÏ†.ObjLiftDomain
  ObjLiftCodomain := F.obj_proj b â–¸ hÏ†.ObjLiftCodomain
  HomLift := âŸ¨by
    rw [congr_hom F.w.symm]
    simp only [Functor.comp_map, Category.assoc, eqToHom_trans, hÏ†.HomLift.1,
      eqToHom_trans_assoc]âŸ©


/-- TWOMORPHISMS -/

structure TwoMorphism {ğ’³ ğ’´ : BasedCategory ğ’®} (F G : Morphism ğ’³ ğ’´) extends
  CategoryTheory.NatTrans F.toFunctor G.toFunctor where
  (aboveId : âˆ€ {a : ğ’³.carrier} {S : ğ’®} (_ : ğ’³.p.obj a = S), IsHomLift ğ’´ (ğŸ™ S) (toNatTrans.app a))

@[ext]
lemma TwoMorphism.ext {ğ’³ ğ’´ : BasedCategory ğ’®} {F G : Morphism ğ’³ ğ’´} (Î± Î² : TwoMorphism F G)
  (h : Î±.toNatTrans = Î².toNatTrans) : Î± = Î² := by
  cases Î±
  cases Î²
  simp at h
  subst h
  rfl

def TwoMorphism.id {ğ’³ ğ’´ : BasedCategory ğ’®} (F : Morphism ğ’³ ğ’´) : TwoMorphism F F := {
  toNatTrans := CategoryTheory.NatTrans.id F.toFunctor
  aboveId := by
    intro a S ha
    constructor
    Â· constructor
      simp only [NatTrans.id_app', map_id, id_comp, comp_id]
    all_goals rwa [â†CategoryTheory.Functor.comp_obj, F.w] }

@[simp]
lemma TwoMorphism.id_toNatTrans {ğ’³ ğ’´ : BasedCategory ğ’®} (F : Morphism ğ’³ ğ’´) : (TwoMorphism.id F).toNatTrans = CategoryTheory.NatTrans.id F.toFunctor := rfl

def TwoMorphism.comp {ğ’³ ğ’´ : BasedCategory ğ’®} {F G H : Morphism ğ’³ ğ’´} (Î± : TwoMorphism F G) (Î² : TwoMorphism G H) :
  TwoMorphism F H := {
    toNatTrans := CategoryTheory.NatTrans.vcomp Î±.toNatTrans Î².toNatTrans
    aboveId := by
      intro a S ha
      rw [CategoryTheory.NatTrans.vcomp_app, show ğŸ™ S = ğŸ™ S â‰« ğŸ™ S by simp only [comp_id]]
      apply IsHomLift_comp (Î±.aboveId ha) (Î².aboveId ha)
  }

@[simp]
lemma TwoMorphism.comp_app {ğ’³ ğ’´ : BasedCategory ğ’®} {F G H : Morphism ğ’³ ğ’´} (Î± : TwoMorphism F G)
    (Î² : TwoMorphism G H) (x : ğ’³.1) : (comp Î± Î²).app x = (Î±.app x) â‰« Î².app x:= rfl

@[simp]
lemma CategoryTheory.NatTrans.id_vcomp {C D : Type _} [Category C] [Category D] {F G : C â¥¤ D}
    (f : NatTrans F G) :
  NatTrans.vcomp (NatTrans.id F) f = f := by
  ext x
  simp only [vcomp_eq_comp, comp_app, id_app', id_comp]

@[simp]
lemma CategoryTheory.NatTrans.vcomp_id {C D : Type _} [Category C] [Category D] {F G : C â¥¤ D}
    (f : NatTrans F G) :
  NatTrans.vcomp f (NatTrans.id G) = f := by
  ext x
  simp only [vcomp_eq_comp, comp_app, id_app', comp_id]

@[simp]
lemma TwoMorphism.comp_toNatTrans {ğ’³ ğ’´ : BasedCategory ğ’®} {F G H : Morphism ğ’³ ğ’´}
    (Î± : TwoMorphism F G) (Î² : TwoMorphism G H) :
    (comp Î± Î²).toNatTrans = NatTrans.vcomp Î±.toNatTrans Î².toNatTrans := rfl

@[simp]
lemma TwoMorphism.id_comp {ğ’³ ğ’´ : BasedCategory ğ’®} {F G : Morphism ğ’³ ğ’´} (Î± : TwoMorphism F G) :
  TwoMorphism.comp (TwoMorphism.id F) Î± = Î± := by
  apply TwoMorphism.ext
  rw [TwoMorphism.comp_toNatTrans, TwoMorphism.id_toNatTrans, CategoryTheory.NatTrans.id_vcomp]

@[simp]
lemma TwoMorphism.comp_id {ğ’³ ğ’´ : BasedCategory ğ’®} {F G : Morphism ğ’³ ğ’´} (Î± : TwoMorphism F G) :
  TwoMorphism.comp Î± (TwoMorphism.id G) = Î± := by
  apply TwoMorphism.ext
  rw [TwoMorphism.comp_toNatTrans, TwoMorphism.id_toNatTrans, CategoryTheory.NatTrans.vcomp_id]

lemma TwoMorphism.comp_assoc {ğ’³ ğ’´ : BasedCategory ğ’®} {F G H I : Morphism ğ’³ ğ’´}
    (Î± : TwoMorphism F G) (Î² : TwoMorphism G H) (Î³ : TwoMorphism H I) :
    TwoMorphism.comp (TwoMorphism.comp Î± Î²) Î³ = TwoMorphism.comp Î± (TwoMorphism.comp Î² Î³):= by
  apply TwoMorphism.ext
  rw [TwoMorphism.comp_toNatTrans, TwoMorphism.comp_toNatTrans, TwoMorphism.comp_toNatTrans, TwoMorphism.comp_toNatTrans, NatTrans.vcomp_eq_comp, NatTrans.vcomp_eq_comp, NatTrans.vcomp_eq_comp, NatTrans.vcomp_eq_comp, assoc]

@[simps]
instance homCategory (ğ’³ ğ’´ : BasedCategory ğ’®) : Category (Morphism ğ’³ ğ’´) where
  Hom := TwoMorphism
  id := TwoMorphism.id
  comp := TwoMorphism.comp
  id_comp := TwoMorphism.id_comp
  comp_id := TwoMorphism.comp_id
  assoc := TwoMorphism.comp_assoc

-- TODO: why do I need this?
@[ext]
lemma homCategory.ext {ğ’³ ğ’´ : BasedCategory ğ’®} {F G : Morphism ğ’³ ğ’´} (Î± Î² : F âŸ¶ G)
    (h : Î±.toNatTrans = Î².toNatTrans) : Î± = Î² := TwoMorphism.ext Î± Î² h

@[simps]
def Morphism.associator {ğ’³ ğ’´ ğ’µ ğ’± : BasedCategory ğ’®} (F : Morphism ğ’³ ğ’´) (G : Morphism ğ’´ ğ’µ)
    (H : Morphism ğ’µ ğ’±) :
  Morphism.comp (Morphism.comp F G) H â‰… Morphism.comp F (Morphism.comp G H) where
    hom := {
      app := fun _ => ğŸ™ _
      aboveId := by
        intro a S ha
        apply IsHomLift_id
        simp only [obj_proj, ha]
    }
    inv := {
      app := fun _ => ğŸ™ _
      aboveId := by
        intro a S ha
        apply IsHomLift_id
        simp only [obj_proj, ha]
    }

@[simps]
def Morphism.leftUnitor {ğ’³ ğ’´ : BasedCategory ğ’®} (F : Morphism ğ’³ ğ’´) :
  Morphism.comp (Morphism.id ğ’³) F â‰… F where
    hom :=
    {
      app := fun a => ğŸ™ (F.obj a)
      naturality := by
        intros
        simp
      aboveId := by
        intro a S ha
        apply IsHomLift_id
        simp only [obj_proj, ha]
    }
    inv := {
      app := fun a => ğŸ™ (F.obj a)
      aboveId := by
        intro a S ha
        apply IsHomLift_id
        simp only [obj_proj, ha]
    }

@[simps]
def Morphism.rightUnitor {ğ’³ ğ’´ : BasedCategory ğ’®} (F : Morphism ğ’³ ğ’´) :
  Morphism.comp F (Morphism.id ğ’´) â‰… F where
    hom :=
    {
      app := fun a => ğŸ™ (F.obj a)
      naturality := by
        intros
        simp
      aboveId := by
        intro a S ha
        apply IsHomLift_id
        simp only [obj_proj, ha]
    }
    inv := {
      app := fun a => ğŸ™ (F.obj a)
      aboveId := by
        intro a S ha
        apply IsHomLift_id
        simp only [obj_proj, ha]
    }

instance : Bicategory (BasedCategory ğ’®) where
  Hom := Morphism
  id := Morphism.id
  comp := Morphism.comp
  homCategory ğ’³ ğ’´ := homCategory ğ’³ ğ’´
  whiskerLeft {ğ’³ ğ’´ ğ’µ} F {G H} Î± := {
      whiskerLeft F.toFunctor Î±.toNatTrans with
      aboveId := by
        intro a S ha
        apply Î±.aboveId
        simp only [Morphism.obj_proj, ha]
    }

  -- TODO: weird that this has non-implicit arguments and above doesnt
  whiskerRight {ğ’³ ğ’´ ğ’µ} F G Î± H := {
    whiskerRight Î±.toNatTrans H.toFunctor with
    aboveId := by
      intro a S ha
      apply Morphism.pres_IsHomLift
      apply Î±.aboveId ha
  }
  associator := Morphism.associator
  leftUnitor {ğ’³ ğ’´} F := Morphism.leftUnitor F
  rightUnitor {ğ’³ ğ’´} F := Morphism.rightUnitor F

instance : Bicategory.Strict (BasedCategory ğ’®) where
  id_comp := Morphism.id_comp
  comp_id := Morphism.comp_id
  assoc := Morphism.assoc


end Based


--instance : Bicategory (Bundled IsFibered)
